#version 430

#define CUBE 0
#define CUBE_T 0
#define SPHERE 0
#define SPHERE_T 0

#define PI 3.1415926535897932384626

layout(local_size_x=512, local_size_y=1, local_size_z=1) in;

struct Boid {
    vec3 pos;
    float a; // useless as fuck
    vec3 dir;
    float b; // useless as fuck
};

layout(std430, binding=0) readonly buffer boids_in {
    Boid boids[];
} In_boids;

layout(std430, binding=1) writeonly buffer boids_out {
    Boid boids[];
} Out_boids;

layout(std430, binding=2) readonly buffer buffer1 {
    // uint cell_start[];
    uvec4 cell_start[];
};

layout(std430, binding=3) readonly buffer buffer2 {
    // uint cell_entries[];
    uvec4 cell_entries[];
};

uniform int boid_count;
uniform float speed;
uniform float map_size;
uniform float view_distance;
uniform float view_angle;

uniform float separation_force;
uniform float alignment_force;
uniform float cohesion_force;

uniform float cell_spacing;
uniform uint table_size;


float distSquared(vec3 A, vec3 B) {
    const vec3 C = A - B;
    return dot(C, C);
}

ivec3 cell_xyz(vec3 pos)
{
    return ivec3(
        floor(pos.x/cell_spacing), floor(pos.y/cell_spacing), floor(pos.z/cell_spacing)
    );
}

uint hash(ivec3 cell_index)
{
    int h = (cell_index.x * 92837111) ^ (cell_index.y * 689287499) ^ (cell_index.z * 283923481);
    return abs(h) % table_size;
}


void main() {
    const uint id = gl_GlobalInvocationID.z * gl_NumWorkGroups.y * gl_NumWorkGroups.x +
                    gl_GlobalInvocationID.y * gl_NumWorkGroups.x +
                    gl_GlobalInvocationID.x;

    if (id >= boid_count) {
        return;
    }

    vec3 ourPos = In_boids.boids[id].pos + (In_boids.boids[id].dir * speed);
    vec3 ourDir = In_boids.boids[id].dir;

    int nb_flockmates = 0;
    vec3 averageDirection = vec3(0.0, 0.0, 0.0);
    vec3 averagePosition = vec3(0.0, 0.0, 0.0);
    vec3 totalForce = vec3(0.0, 0.0, 0.0);


    ivec3 low = cell_xyz(vec3(ourPos.x-view_distance, ourPos.y-view_distance, ourPos.z-view_distance));
    ivec3 high = cell_xyz(vec3(ourPos.x+view_distance, ourPos.y+view_distance, ourPos.z+view_distance));
    // ivec3 pos = cell_xyz(ourPos);

    uint hash_index;
    uint start;
    uint end;

    for (int x = low.x; x <= high.x; ++x) {
        for (int y = low.y; y <= high.y; ++y) {
            for (int z = low.z; z <= high.z; ++z) {


                // hash_index = hash(pos);
                hash_index = hash(ivec3(x, y, z));

                // uint i1 = uint(hash_index / 4);
                // uint i2 = hash_index % 4;

                // start = cell_start[i1][i2];
                start = cell_start[hash_index][0];
                end = start;
                if (hash_index + 1 < boid_count) {
                    // end = cell_start[i1][i2 + 1];
                    end = cell_start[hash_index + 1][0];
                }

                for (uint j = start ; j < end ; ++j) {
                    // uint ii1 = uint(j / 4);
                    // uint ii2 = j % 4;
                    const uint i = cell_entries[j][0];
                    if (id == i) continue; // don't check if itself

                    const vec3 theirPos = In_boids.boids[i].pos;
                    const vec3 theirDir = In_boids.boids[i].dir;

                    const vec3 p = theirPos;
                    const vec3 v = ourPos;
                    const vec3 d = ourDir;
                    const float r = dot((p - v)/length(p - v), d/length(d));

                    const float sqrDist = distSquared(ourPos, theirPos);
                    if (sqrDist < view_distance * view_distance) {
                        if (length(acos(r)) <= view_angle) {
                            nb_flockmates += 1;
                            averageDirection += theirDir;
                            averagePosition += theirPos;

                            // separation
                            totalForce += (ourPos - theirPos) / sqrDist;
                        }
                    }
                }
            }
        }
    }

    /*
    for (int i = 0 ; i < boid_count ; ++i) {
        if (id == i) continue; // don't check if itself

        const vec3 theirPos = In_boids.boids[i].pos;
        const vec3 theirDir = In_boids.boids[i].dir;

        // tests
        const vec3 p = theirPos;
        const vec3 v = ourPos;
        const vec3 d = ourDir;
        const float r = dot((p - v)/length(p - v), d/length(d));

        const float sqrDist = distSquared(ourPos, theirPos);
        if (sqrDist < view_distance * view_distance) {
            if (length(acos(r)) <= view_angle) {
                nb_flockmates += 1;
                averageDirection += theirDir;
                averagePosition += theirPos;

                // separation
                totalForce += (ourPos - theirPos) / sqrDist;
            }
        }
    }
    */

    // separation
    ourDir += totalForce * separation_force;

    if (nb_flockmates != 0) {
        // alignment
        averageDirection /= nb_flockmates;
        ourDir += averageDirection * alignment_force;

        // cohesion
        averagePosition /= nb_flockmates;
        ourDir += -(ourPos - averagePosition) * cohesion_force;
    }

    // border limit
    #if (CUBE == 1)
        const vec3 np = ourPos + (ourDir * speed);
        const float mp2 = map_size * 0.5;
        if (np.x < -mp2 || np.x > mp2 || np.y < -mp2 || np.y > mp2 || np.z < -mp2 || np.z > mp2) {
            const vec3 to_center = normalize(vec3(0, 0, 0) - ourPos);
            ourDir += to_center * 0.1;
        }
    #endif

    #if (CUBE_T == 1)
        const float map_size2 = map_size/2;
        if (ourPos.x < -map_size2)
            ourPos.x = map_size2;
        else if (ourPos.x > map_size2)
            ourPos.x = -map_size2;

        if (ourPos.y < -map_size2)
            ourPos.y = map_size2;
        else if (ourPos.y > map_size2)
            ourPos.y = -map_size2;

        if (ourPos.z < -map_size2)
            ourPos.z = map_size2;
        else if (ourPos.z > map_size2)
            ourPos.z = -map_size2;
    #endif

    #if (SPHERE == 1)

        if (length(ourPos) > map_size) {
            const vec3 to_center = normalize(vec3(0, 0, 0) - ourPos);
            ourDir += to_center * 0.1;
        }
    #endif

    #if (SPHERE_T == 1)
        if (length(ourPos) > map_size) {
            ourPos = -normalize(ourPos) * map_size;
        }
    #endif

    Out_boids.boids[id].pos = ourPos;
    Out_boids.boids[id].dir = normalize(ourDir);
}
